第二个手工汇编程序：

```
#PURPOSE:  This program finds the maximum number of a
#          set of data items.
#
#VARIABLES: The registers have the following uses:
#
# %edi - Holds the index of the data item being examined
# %ebx - Largest data item found
# %eax - Current data item
#
# The following memory locations are used:
#
# data_items - contains the item data.  A 0 is used
#              to terminate the data
.section .data

data_items:                       #These are the data items
.long 3,67,34,222,45,75,54,34,44,33,22,11,66,0

.section .text

.globl _start
_start:
movl $0, %edi             		# move 0 into the index register
movl data_items(,%edi,4), %eax  # load the first byte of data
movl %eax, %ebx           		# since this is the first item, %eax is
                                # the biggest

start_loop:                   # start loop
cmpl $0, %eax                 # check to see if we've hit the end
je loop_exit
incl %edi                     # load next value
movl data_items(,%edi,4), %eax
cmpl %ebx, %eax               # compare values
jle start_loop                # jump to loop beginning if the new
                              # one isn't bigger
movl %eax, %ebx               # move the value as the largest
jmp start_loop                # jump to loop beginning

loop_exit:
# %ebx is the status code for the exit system call
# and it already has the maximum number
    movl $1, %eax             #1 is the exit() syscall
    int  $0x80
```

这个程序中数据段中不再是空了，而是有数据的，定义了data\_items数据列表，可以认为数组，最后一项为0，表示数组的结束。数据段中使用.long来规定所有数据都要占用4个字节，这种数据占用的伪指令还有几个：.int（占用2个字节）、.byte（占用1个字节）。还有一个指令比较特殊，是用于字符串的：.ascii，用法是：.ascii "hello\0"，其中每个字符占用1个字节。

这段代码是用来计算data\_items中最大值的，各个寄存器的使用说明如下：

```
# %edi - Holds the index of the data item being examined
# %ebx - Largest data item found
# %eax - Current data item

```

这里重点要说明的是：寻址模式。linux汇编中内存地址的访问模式

